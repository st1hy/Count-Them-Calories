apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.github.st1hy.countthemcalories"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 21
        versionName "1.3.2"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix " DEV"
        }
    }

    productFlavors {
        //Enable runtime development utilities such as stetho, leakcanary or tinydancer
        devtools {}
        production {}
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
    }

    applicationVariants.all { variant ->
        variant.outputs.each {
            output ->
                def String newApkName = "Count Them Calories " +
                        "${variant.versionName} (${variant.versionCode}).apk"

                def String dir = output.outputFile.parent;
                output.outputFile = new File(dir, newApkName)
        }
    }

    dexOptions {
        preDexLibraries true
        javaMaxHeapSize "3g"
        dexInProcess = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        //Retrolamda breaks lint
        //checkReleaseBuilds false
        abortOnError false
    }
}

configurations {
    devtoolsDebugCompile
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    compile project(':database')
    compile "com.android.support:support-annotations:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.jakewharton.timber:timber:$timberVersion"
    compile "io.reactivex:rxandroid:$rxAndroidVersion"
    compile "io.reactivex:rxjava:$rxJavaVersion"
    compile "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    compile "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"
    compile "com.squareup.picasso:picasso:$picassoVersion"
    compile "com.jakewharton.rxbinding:rxbinding:$rxBindingsVersion"
    compile "com.jakewharton.rxbinding:rxbinding-appcompat-v7:$rxBindingsVersion"
    compile "com.jakewharton.rxbinding:rxbinding-support-v4:$rxBindingsVersion"
    compile "com.jakewharton.rxbinding:rxbinding-recyclerview-v7:$rxBindingsVersion"
    compile "com.google.guava:guava:$guavaVersion"
    compile "com.splitwise:tokenautocomplete:$tokenAutoCompleteVersion"
    compile "org.parceler:parceler-api:$parcelerVersion"
    annotationProcessor "org.parceler:parceler:$parcelerVersion"
    provided "com.google.code.findbugs:jsr305:3.0.0"

    compile 'com.l4digital.fastscroll:fastscroll:1.0.1'
    compile 'com.beloo.widget:ChipsLayoutManager:0.3.7@aar'

    devtoolsDebugCompile "com.facebook.stetho:stetho:$stethoVersion"
    devtoolsDebugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    devtoolsDebugCompile "com.github.brianPlummer:tinydancer:$tinyDancerVersion"

    androidTestCompile "com.android.support:support-annotations:$supportLibraryVersion"
    androidTestCompile "com.android.support.test:runner:$supportTestRunnerVersion"
    androidTestCompile "com.android.support.test:rules:$supportTestRunnerVersion"
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espressoVersion") {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-intents:$espressoVersion") {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$espressoVersion") {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    androidTestCompile "org.mockito:mockito-core:$mockitoVersion"
    androidTestCompile "junit:junit:$jUnitVersion"
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    androidTestCompile "com.android.support.test.uiautomator:uiautomator-v18:$uiAutomatorVersion"

    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile "junit:junit:$jUnitVersion"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    testCompile "com.android.support:support-annotations:$supportLibraryVersion"
    testCompile "com.android.support.test.espresso:espresso-intents:$espressoVersion"
}
