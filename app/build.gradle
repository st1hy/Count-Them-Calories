apply plugin: 'idea'
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.github.st1hy.countthemcalories"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 6
        versionName "0.6"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix " DEV"
        }
    }

    productFlavors {
        //Enable runtime development utilities such as stetho, leakcanary or tinydancer
        devtools {}
        production {}
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
    }

    applicationVariants.all { variant ->
        variant.outputs.each {
            output ->
                def String newApkName = "Count Them Calories $variant.versionName" + ".apk"

                def String dir = output.outputFile.parent;
                output.outputFile = new File(dir, newApkName)
        }
    }

    dexOptions {
        preDexLibraries true
        javaMaxHeapSize "3g"
        incremental true
        dexInProcess = true
    }
}

configurations {
    devtoolsDebugCompile
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    final supportLibraryVersion = rootProject.ext.supportLibraryVersion
    final daggerVersion = rootProject.ext.daggerVersion
    final rxJavaVersion = rootProject.ext.rxJavaVersion
    final rxAndroidVersion = rootProject.ext.rxAndroidVersion
    final timberVersion = rootProject.ext.timberVersion
    final butterknifeVersion = '8.0.1'

    final jUnitVersion = rootProject.ext.jUnitVersion
    final robolectricVersion = rootProject.ext.robolectricVersion
    final supportTestRunnerVersion = '0.5'
    final mockitoVersion = rootProject.ext.mockitoVersion
    final espressoVersion = '2.2.2'
    final hamcrestVersion = '2.0.0.0'
    final picassoVersion = '2.5.2'

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile project(':database')
    compile "com.android.support:support-annotations:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.jakewharton.timber:timber:$timberVersion"
    compile "io.reactivex:rxandroid:$rxAndroidVersion"
    compile "io.reactivex:rxjava:$rxJavaVersion"
    compile "com.google.dagger:dagger:$daggerVersion"
    apt "com.google.dagger:dagger-compiler:$daggerVersion"
    compile "com.jakewharton:butterknife:$butterknifeVersion"
    apt "com.jakewharton:butterknife-compiler:$butterknifeVersion"
    compile "com.squareup.picasso:picasso:$picassoVersion"
//    compile 'org.lucasr.twowayview:twowayview:0.1.4' //customize recycler layout
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-support-v4:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.4.0'
    compile 'com.google.guava:guava:19.0'

    devtoolsDebugCompile 'com.facebook.stetho:stetho:1.3.1'
    devtoolsDebugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    devtoolsDebugCompile 'com.github.brianPlummer:tinydancer:0.0.9'

    androidTestCompile "com.android.support:support-annotations:$supportLibraryVersion"
    androidTestCompile "com.android.support.test:runner:$supportTestRunnerVersion"
    androidTestCompile "com.android.support.test:rules:$supportTestRunnerVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$espressoVersion") {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-v4'
    }
    androidTestCompile "org.mockito:mockito-core:$mockitoVersion"
    androidTestCompile "junit:junit:$jUnitVersion"
    androidTestApt "com.google.dagger:dagger-compiler:$daggerVersion"
//    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'

    testCompile "org.robolectric:robolectric:$robolectricVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile "junit:junit:$jUnitVersion"
    testCompile "org.hamcrest:hamcrest-junit:$hamcrestVersion"
    testApt "com.google.dagger:dagger-compiler:$daggerVersion"
    testCompile "com.android.support:support-annotations:$supportLibraryVersion"
    testCompile "com.android.support.test.espresso:espresso-intents:$espressoVersion"
}
